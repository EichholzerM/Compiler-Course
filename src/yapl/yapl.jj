/**** YAPL Compiler
*
*	Copyright 2010 Daniel Hölbling, Klagenfurt University
*
*	Licensed under the Apache License, Version 2.0 (the "License");
*	you may not use this file except in compliance with the License.
*	You may obtain a copy of the License at
*
*		http://www.apache.org/licenses/LICENSE-2.0
*       
*	Unless required by applicable law or agreed to in writing, software
*	distributed under the License is distributed on an "AS IS" BASIS,
*	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
*	See the License for the specific language governing permissions and
*	limitations under the License.
***/
options {
  JDK_VERSION = "1.5";
  static = true;
}
PARSER_BEGIN(yapl)
package yapl;
import yapl.interfaces.*;
import yapl.lib.*;
import java.util.*;
import yapl.exceptions.*;
import yapl.interfaces.Symbol.SymbolKind;
import yapl.types.*;
public class yapl {
  private static Symboltable symbolTable = new SymboltableImpl();
  private static CodeGen codeGen;

  public void init(BackendBinSM backend)
  {    symbolTable = new SymboltableImpl();
  	codeGen = new CodeGenImpl(backend); 
  }
}
PARSER_END(yapl)

SKIP :
{
 	" "
|	"\r"
|	"\t"
|	"\n"
}

SPECIAL_TOKEN :
{
	< COMMENT : ("/*" (~["*"] | "*" ~["/"])* "*/") >
}

TOKEN:
{
  <# LETTER : (["A"-"Z"] | ["a"-"z"] | "_" ) > |
  <# DIGIT : (["0"-"9"]) > |
  <# OTHERCHAR: ("+" | "-" | "*" | "/" | "." | "," | ";" | ":" | "!"
| "?" | "=" | "#" | "%" | "<" | ">" | "$" | "(" | ")"
| "[" | "]" | "{" | "}" | "\\" | "@" | "&" | "^" | "|") >}



TOKEN :
{
  	< NEW: "new" > |
	< AND: "And" > |
	< OR: "Or" > |
	< DECLARE : "Declare" > |
	< PROGRAM : "Program" > |
	< BEGIN : "Begin" > |
	< END: "End" > |
	< IF : "If" > |
	< THEN : "Then" > |
	< ELSE : "Else" > |
	< ENDIF : "EndIf" > |
	< WHILE : "While" > |
	< CONST : "Const" > |
	< DO : "Do" > |
	< ENDWHILE : "EndWhile" > |
	< RETURN : "Return" > |
	< WRITE : "Write" > |
	< READONLY : "Readonly" > |
	< PROCEDURE : "Procedure" > |
	< TRUE : "True" > |
	< FALSE : "False" > |
	< INTEGER :  "Integer" > |
  	< BOOLEAN : "Boolean" > |	< IDENT : < LETTER > (< LETTER > | < DIGIT >)* > |
	< NUMBER: (< DIGIT >)+ > |
	< STRING: "\"" ( " " | < LETTER > | < DIGIT > | < OTHERCHAR > )* "\"" > 
}

Attrib Literal() throws YAPLException :
{
  Type t;
  Attrib a;
  Token tok;}
{
  < TRUE >  {
    a = new AttribImpl(new BooleanType());
    a.setOffset(codeGen.storeConst(true));
    return a;  } | < FALSE >
  {
    a = new AttribImpl(new BooleanType());
    a.setOffset(codeGen.storeConst(false));
    return a;  } | tok = < NUMBER >
  {
    a = new AttribImpl(new IntegerType());
    a.setOffset(codeGen.storeConst(Integer.parseInt(tok.image)));
    return a;  }}

void PredefinedProcedures() throws YAPLException :
{}
{
  (PredefProc())*}
void PredefProc() throws YAPLException :
{
  Token t;
  Type type = null;
  List<Symbol> parameters = null; 
}
{
  < PROCEDURE > t = < IDENT >
  {
    Symbol symbol = new SymbolImpl(t.image, Symbol.SymbolKind.Procedure);
    symbolTable.addSymbol(symbol);
    symbolTable.openScope(true);
    symbolTable.setParentSymbol(symbol);
  } "(" (parameters = FormalParamList())? ")"
  (":" type = Type())? ";"
  {
    if (parameters != null)
    	symbol.setParameters(parameters);
    symbol.setType(type);
    symbolTable.closeScope();
  }}

Type Type() throws YAPLException :
{
  Type type;
}{
  (< INTEGER >  {    type = new IntegerType();  }  | < BOOLEAN >  {
    type = new BooleanType();  }) ( "[" "]"  {
    type = new ArrayType(type);  })*
  {
    return type;  }}
Attrib Selector(Attrib a) throws YAPLException :
{
Token errorToken;
Token errorToken2;
Attrib index;}
{
  errorToken = "[" index = Expr() errorToken2 = "]" (a = Selector(a))?
  {
    codeGen.verifyArraySelector(index, errorToken2);
    if (a.getType() instanceof ArrayType)    {
      return new AttribImpl(((ArrayType)a.getType()).getElementType());    }    
    throw new SelectorNotArrayException(errorToken);  }}

Attrib ArrayLen() throws YAPLException :
{ Token t;
Token errorToken;
Attrib retVal; }{
  errorToken = "#" t = < IDENT >  {
    Symbol symbol = symbolTable.lookup(t.image);
    if (symbol.getKind() != SymbolKind.Variable && symbol.getKind() != SymbolKind.Parameter)
    	throw new SymbolIllegalUseException(symbol);
    retVal = new AttribImpl(symbol.getType());  } (retVal = Selector(retVal))?
  {
    if (!(retVal.getType() instanceof ArrayType))
    {
      throw new ArrayLenNotArrayException(errorToken);
    }
    return new AttribImpl(new IntegerType());  }}
Attrib PrimaryExpr() throws YAPLException :
{
  Token t;
  Attrib retVal = null;
  Attrib type;
}{
  (retVal = Literal()  {
    codeGen.loadConstData(retVal.getOffset());
    return retVal;  } | "(" retVal = Expr() ")" | LOOKAHEAD(2) retVal = ProcedureCall(true) | t = < IDENT >
  {
    Symbol symbol = symbolTable.lookup(t.image);
    if (symbol.getKind() == SymbolKind.Procedure)    	throw new SymbolIllegalUseException(symbol);
    if (symbol.getKind() == SymbolKind.Constant)
    	codeGen.loadConstData(symbol.getOffset());
    if (symbol.getKind() == SymbolKind.Variable)
    	codeGen.loadVariable(symbol);
    if (symbol.getKind() == SymbolKind.Parameter)
    	codeGen.loadVariable(symbol);
    retVal = new AttribImpl(symbol.getType());  } (retVal = Selector(retVal))? | retVal = ArrayLen())

  {
    return retVal;  }}
Attrib ProcedureCall(boolean isFunction) throws YAPLException :
{ Token t;
Attrib retVal;
List<Attrib> arguments = new ArrayList<Attrib>();
Token errorToken; }
{
  t = < IDENT >
  {
    Symbol symbol = symbolTable.lookup(t.image);
    if (symbol.getKind() != SymbolKind.Procedure)
    	throw new SymbolIllegalUseException(symbol);
    if (isFunction && symbol.getType() == null)
    	throw new ProcNotFuncExprException(symbol);  } "(" (arguments = ArgumentList(symbol))? errorToken = ")"

  {
    if (symbol.getParameters().size() != arguments.size())
    	throw new TooFewArgsException(symbol, errorToken);
    Attrib[] arr = new Attrib[arguments.size()];
	arr = arguments.toArray(arr);
	return codeGen.callProc(symbol, arr);  }}
List<Attrib> ArgumentList(Symbol s) throws YAPLException:
{
	int argumentCount = 1;
	List<Symbol> parameters = s.getParameters();
	Attrib arg;
	List<Attrib> retVal = new ArrayList<Attrib>();
}
{
  arg = Expr()
  {
    retVal.add(arg);
    if ((parameters.size() < argumentCount) || (!parameters.get(argumentCount-1).getType().isCompatible(arg.getType())))
    	throw new ArgNotApplicableException(argumentCount, s, token);
	if (arg.getType() instanceof ArrayType && arg.getType().isReadonly() && !parameters.get(argumentCount-1).getType().isReadonly())
		throw new ReadonlyArgException(argumentCount, s, token);      } ("," arg = Expr()  {
    retVal.add(arg);
    argumentCount++;
    if ((parameters.size() < argumentCount) || (!parameters.get(argumentCount-1).getType().isCompatible(arg.getType())))
    	throw new ArgNotApplicableException(argumentCount, s, token);
	if (arg.getType() instanceof ArrayType && arg.getType().isReadonly() && !parameters.get(argumentCount-1).getType().isReadonly())
		throw new ReadonlyArgException(argumentCount, s, token);  })*
  {
    return retVal;  }}
Attrib UnaryExpr () throws YAPLException:
{
  Token t = null;
  Attrib op;
}
{
  (t = AddOp())? op = PrimaryExpr()
  {
    return codeGen.op1(t, op);  }    }
Attrib MulExpr () throws YAPLException:
{
  Attrib retVal;
  Token t;
  Attrib op2;}
{
  (retVal = UnaryExpr()) (t = MulOp() op2 = UnaryExpr()
  {
    retVal = codeGen.op2(retVal, t, op2);  })*
  {
    return retVal;  }    }
Attrib AddExpr () throws YAPLException:
{
  Attrib retVal;
  Token t;
  Attrib op2;}
{
  retVal = MulExpr() (t = AddOp() op2 = MulExpr()  {
    retVal = codeGen.op2(retVal, t, op2);  })*
  {
    return retVal;   }}

Attrib RelExpr () throws YAPLException:
{
  Attrib retVal;
  Token t;
  Attrib op2;}
{
  retVal = AddExpr() (t = RelOp() op2 = AddExpr()
  {
    retVal = codeGen.relOp(retVal, t, op2);  })*
  {
    return retVal;  }}

Token AddOp () throws YAPLException:
{
  Token t;
}
{
  (t = "+" | t = "-")
  {
    return t;  }}

Token MulOp () throws YAPLException:
{
  Token t;}
{
  (t = "*" | t = "/" | t = "%")
  {
    return t;  }}

Token RelOp() throws YAPLException:
{Token t;}
{
   (t = "<" | t = "<=" | t = ">=" | t = ">")
   {
     return t;   }}

Token EqualOp() throws YAPLException :
{
  Token t;
}
{
    (t = "==" | t = "!=")
    {
      return t;    }}
Attrib EqualExpr () throws YAPLException:
{
  Attrib retVal;
  Token op;
  Attrib op2;
}
{
  retVal = RelExpr() (op = EqualOp() op2 = RelExpr()
  {
    retVal = codeGen.equalOp(retVal, op, op2);  })*
  {
    return retVal;  }}

Attrib CondAndExpr () throws YAPLException:
{
  Attrib retVal;
  Token op;
  Attrib expr2;
}
{
  retVal = EqualExpr() (op = < AND > expr2 = EqualExpr()
  {
    retVal = codeGen.op2(retVal, op, expr2);  })*
  {
    return retVal;  }}
Attrib Expr() throws YAPLException:
{
  Attrib retVal;
  Token op;
  Attrib op2;}{
  ((retVal = CondAndExpr() (op = < OR > op2 = CondAndExpr()
  {
    retVal = codeGen.op2(retVal, op, op2);   })*) | retVal = CreationExpr())
  {
    return retVal;  }}
Attrib CreationExpr() throws YAPLException:
{
  Attrib retVal;
  Type type;
  Token errorToken;
  Attrib index;
  int arrayDim = 0;
}{
  < NEW > (<INTEGER >  {
    type = new IntegerType();  }|< BOOLEAN >  {
    type = new BooleanType();  }) "[" index = Expr() errorToken = "]"  {
    codeGen.verifyArraySelector(index, errorToken);
    codeGen.storeArrayDim(arrayDim, index);
    type = new ArrayType(type);
    arrayDim++;  } ("[" index = Expr() errorToken = "]"  {
    codeGen.verifyArraySelector(index, errorToken);
    codeGen.storeArrayDim(arrayDim, index);
    type = new ArrayType(type);
    arrayDim++;  })*
  {
    return codeGen.allocArray((ArrayType)type);  }}

void ConstDecl() throws YAPLException :
{ Token t;
  Attrib attrib;
}{
  < CONST > t = <IDENT >
  {
    SymbolImpl sym = new SymbolImpl(t.image, SymbolKind.Constant);
    symbolTable.addSymbol(sym);  } "=" attrib = Literal()  {
    sym.setOffset(attrib.getOffset());
    sym.setType(attrib.getType());  } ";"}

void VarDecl() throws YAPLException :
{
  Token t;
  ArrayList<Symbol> symbols = new ArrayList<Symbol>();
  Symbol sym;
  Type type;
}{
  t = < IDENT >
  {
    sym = new SymbolImpl(t.image, Symbol.SymbolKind.Variable);
    symbols.add(sym);
    symbolTable.addSymbol(sym);      }
  ("," t = < IDENT >
  {
    sym = new SymbolImpl(t.image, Symbol.SymbolKind.Variable);
    symbols.add(sym);
    symbolTable.addSymbol(sym);  })* ":" type = Type() ";"
  {
    for(Symbol s : symbols)    {
		s.setType(type);
		codeGen.allocVariable(s);     }  }}

void Decl() throws YAPLException :
{}
{
  < DECLARE > ( ConstDecl() | VarDecl() )*}

void Assignment() throws YAPLException :
{
  Token t;
  Token t2;
  Attrib target;
  Attrib source;
}{
  t = < IDENT >  {
    Symbol symbol = symbolTable.lookup(t.image);
    target = new AttribImpl(symbol);
    if (symbol.getKind() != SymbolKind.Variable && symbol.getKind() != SymbolKind.Parameter)
    	throw new SymbolIllegalUseException(symbol);  } (target = Selector(target))? t2 = ":=" source = Expr()
  {
    if (target.getType().isReadonly())
		throw new ReadonlyAssignException(t);
    TypeMismatchAssignException.errorToken = t2;
    codeGen.assign(target, source);  }}
void IfStatement() throws YAPLException :
{
  Attrib clause;
  String endLabel = codeGen.newLabel();
  String elseLabel = codeGen.newLabel();
}
{
  < IF > clause = Expr()  {
    if (!(clause.getType() instanceof BooleanType))
      	throw new CondNotBoolException(token);

    codeGen.branchIfFalse(clause, elseLabel);  } < THEN > StatementList()
  {
    codeGen.jump(endLabel);
    codeGen.assignLabel(elseLabel);
  }  ( < ELSE > StatementList())? < ENDIF >
  {
    codeGen.assignLabel(endLabel);  }}

void WhileStatement() throws YAPLException :
{
  Attrib clause;
  String startLabel = codeGen.newLabel();
  String endLabel = codeGen.newLabel();
}{
  {
    codeGen.assignLabel(startLabel);  }
  < WHILE > clause = Expr()  {
    if (!(clause.getType() instanceof BooleanType))
      	throw new CondNotBoolException(token);
    codeGen.branchIfFalse(clause, endLabel);   } < DO > StatementList()
  {
    codeGen.jump(startLabel);
    codeGen.assignLabel(endLabel);  }< ENDWHILE >}

void ReturnStatement() throws YAPLException :
{
  Attrib a = null;
  Symbol s = symbolTable.getNearestParentSymbol(SymbolKind.Procedure);
  boolean returnedValue = false;
}{
  < RETURN > (a = Expr()  {
    returnedValue = true;
    if (s == null)
      throw new IllegalRetValMainException(token);
    if (s.getType() == null)
    	throw new IllegalRetValProcException(s, token);
    if (!s.getType().isCompatible(a.getType()))
    	throw new InvalidReturnTypeException(s, token);   })?
  {
    if (s == null) return;
    if (s.getType() != null && !returnedValue)
    	throw new InvalidReturnTypeException(s, token); 
    s.setReturnSeen(true);
    codeGen.returnFromProc(s, a);  }}

void WriteStatement() throws YAPLException :
{
  Token s;
}{
  < WRITE > s = < STRING >
  {
    codeGen.writeString(s.image);  }}

void Statement() throws YAPLException :{}{
  IfStatement() | WhileStatement() | ReturnStatement() | WriteStatement() | LOOKAHEAD(2) Assignment() |
  ProcedureCall(false) | Block()}

void StatementList() throws YAPLException :
{}
{
  (Statement() ";")*}

void Block() throws YAPLException :
{
  Token errorSymbol;
}
{
  {
    symbolTable.openScope(false);  }
  (Decl())? < BEGIN > StatementList() errorSymbol = < END >
  {
    Symbol parent = symbolTable.getNearestParentSymbol(SymbolKind.Procedure);
    if (parent != null && parent.getType() != null && !parent.getReturnSeen())
    	throw new MissingReturnException(parent, errorSymbol);
    symbolTable.closeScope();  }}

List<Symbol> FormalParam() throws YAPLException :
{
  Token t;
  Token read = null;
  Type type;
  Symbol sym;
  ArrayList<Symbol> symbols = new ArrayList<Symbol>();
  boolean readOnly = false;
}
{
  t = < IDENT >  {
    sym = new SymbolImpl(t.image, SymbolKind.Parameter);
    symbolTable.addSymbol(sym);
    symbols.add(sym);  }("," t = < IDENT >  {
    sym = new SymbolImpl(t.image, SymbolKind.Parameter);
    symbolTable.addSymbol(sym);
    symbols.add(sym);  })* ":" (read = < READONLY >  {
    readOnly = true;  })? type = Type()
  {
    if (readOnly && !(type instanceof ArrayType))
    	throw new ReadonlyNotArrayException(read);
    int i = 0;
    for(Symbol s : symbols)
    {
      s.setType(type);
      type.setReadonly(readOnly);
      s.setOffset(i);
      i++;    }
    return symbols;  }}

List<Symbol> FormalParamList() throws YAPLException :
{
  List<Symbol> parameters;
  List<Symbol> current;
}{
  parameters = FormalParam() (";" current = FormalParam()  {
    for(Symbol s : current)    {		parameters.add(s);
    }  })*
  {
    return parameters;  }}

void Procedure() throws YAPLException :
{ Token t;
Type type;
List<Symbol> parameters = null;
}
{
  < PROCEDURE > t = < IDENT >  {
    Symbol symbol = new SymbolImpl(t.image, SymbolKind.Procedure);
    symbolTable.addSymbol(symbol);
    symbolTable.openScope(false);
    symbolTable.setParentSymbol(symbol);  } "(" (parameters = FormalParamList())? ")"
  {
    if (parameters != null)
    	symbol.setParameters(parameters);  }
  (":" type = Type()  {
    symbol.setType(type);  })?
  {
    codeGen.enterProc(symbol);  }
  Block() t = < IDENT >
  {
    codeGen.exitProc(symbol);
    Symbol parent = symbolTable.getNearestParentSymbol(SymbolKind.Procedure);
    if (!parent.getName().equals(t.image))    	throw new EndIdentifierMismatchException(parent, t.image); 
    symbolTable.closeScope();  }
  ";"}

void Program() throws YAPLException :
{}
{
  {
    Token t;
    Token x;  }
  < PROGRAM > t = <IDENT >  {
    Symbol symbol = new SymbolImpl(t.image, SymbolKind.Program);
    symbol.setGlobal(true);
    symbolTable.openScope(true);
    symbolTable.setParentSymbol(symbol);  } (Decl() | Procedure())*
  < BEGIN >  {
    codeGen.enterProc(symbol);  } StatementList() < END > x = <IDENT >
  {
    codeGen.exitProc(symbol);
    if (!t.image.equals(x.image))
    	throw new EndIdentifierMismatchException(symbol, x.image); 
    symbolTable.closeScope();  }
  "."}

void Start() throws YAPLException :{  Token t;}{
  {
    symbolTable.setDebug(false);  }
  (    Program()
  )*
  <EOF>}