/**** YAPL Compiler
*
*	Copyright 2010 Daniel Hölbling, Klagenfurt University
*
*	Licensed under the Apache License, Version 2.0 (the "License");
*	you may not use this file except in compliance with the License.
*	You may obtain a copy of the License at
*
*		http://www.apache.org/licenses/LICENSE-2.0
*       
*	Unless required by applicable law or agreed to in writing, software
*	distributed under the License is distributed on an "AS IS" BASIS,
*	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
*	See the License for the specific language governing permissions and
*	limitations under the License.
***/
options {
  JDK_VERSION = "1.5";
  static = false;
}
PARSER_BEGIN(yapl)
package yapl;
import yapl.interfaces.*;
import yapl.lib.*;
import java.util.*;
public class yapl {
  private Symboltable symbolTable = new SymboltableImpl();

  public void clear()  {  	symbolTable = new SymboltableImpl();
  }
}
PARSER_END(yapl)

SKIP :
{
 	" "
|	"\r"
|	"\t"
|	"\n"
}

SPECIAL_TOKEN :
{
	< COMMENT : ("/*" (~["*"] | "*" ~["/"])* "*/") >
}

TOKEN:
{
  <# LETTER : (["A"-"Z"] | ["a"-"z"] | "_" ) > |
  <# DIGIT : (["0"-"9"]) > |
  <# OTHERCHAR: ("+" | "-" | "*" | "/" | "." | "," | ";" | ":" | "!"
| "?" | "=" | "#" | "%" | "<" | ">" | "$" | "(" | ")"
| "[" | "]" | "{" | "}" | "\\" | "@" | "&" | "^" | "|") >}



TOKEN :
{
  	< NEW: "new" > |
	< AND: "And" > |
	< OR: "Or" > |
	< DECLARE : "Declare" > |
	< PROGRAM : "Program" > |
	< BEGIN : "Begin" > |
	< END: "End" > |
	< IF : "If" > |
	< THEN : "Then" > |
	< ELSE : "Else" > |
	< ENDIF : "EndIf" > |
	< WHILE : "While" > |
	< CONST : "Const" > |
	< DO : "Do" > |
	< ENDWHILE : "EndWhile" > |
	< RETURN : "Return" > |
	< WRITE : "Write" > |
	< READONLY : "Readonly" > |
	< PROCEDURE : "Procedure" > |
  	< Literal: "True" | "False" | < NUMBER > > |
  	< PrimType : "Integer" | "Boolean" > |	< IDENT : < LETTER > (< LETTER > | < DIGIT >)* > |
	< NUMBER: (< DIGIT >)+ > |
	< STRING: "\"" ( " " | < LETTER > | < DIGIT > | < OTHERCHAR > )* "\"" > 
}

void PredefinedProcedures() throws YAPLException :
{}
{
  PredefProc()}
void PredefProc() throws YAPLException :
{
  Token t;
}
{
  < PROCEDURE > t = < IDENT >
  {
    Symbol symbol = new SymbolImpl(t.image, Symbol.SymbolKind.Procedure);
    symbolTable.addSymbol(symbol);
    symbolTable.openScope(false);
  } "(" (FormalParamList())? ")"
  (":" Type())? ";"
  {
    symbolTable.closeScope();
  }}

void Type() throws YAPLException :
{}{
  < PrimType > ( "[" "]")*}
void Selector() throws YAPLException :
{}
{
  "[" Expr() "]" (Selector())?}

void ArrayLen() throws YAPLException :
{}{
  "#" < IDENT > (Selector())?}
void PrimaryExpr() throws YAPLException :
{}{
  (< Literal > | "(" Expr() ")" | LOOKAHEAD(2) ProcedureCall() | < IDENT > (Selector())? | ArrayLen())}
void ProcedureCall() throws YAPLException :
{}
{
  < IDENT > "(" (ArgumentList())? ")"}
void ArgumentList() throws YAPLException:
{}
{
  Expr() ("," Expr())*}
void UnaryExpr () throws YAPLException:
{}
{
  (AddOp())? PrimaryExpr()    }
void MulExpr () throws YAPLException:
{}
{
  (UnaryExpr()) (MulOp() UnaryExpr())*    }
void AddExpr () throws YAPLException:
{}
{
  MulExpr() (AddOp() MulExpr())*}

void RelExpr () throws YAPLException:
{}
{
  AddExpr() (RelOp() AddExpr())*}

void AddOp () throws YAPLException:
{}
{
  "+" | "-"}

void MulOp () throws YAPLException:
{}
{
  "*" | "/" | "%"}

void RelOp() throws YAPLException:
{}
{
   "<" | "<=" | ">=" | ">"}

void EqualOp() throws YAPLException :
{}
{
    "==" | "!="}
void EqualExpr () throws YAPLException:
{}
{
  RelExpr() (EqualOp() RelExpr())*}

void CondAndExpr () throws YAPLException:
{}
{
  EqualExpr() (< AND > EqualExpr())*}
void Expr() throws YAPLException:
{}{
  (CondAndExpr() (< OR > CondAndExpr())*) | CreationExpr()}
void CreationExpr() throws YAPLException:
{}{
  < NEW > < PrimType > "[" Expr() "]" ("[" Expr() "]")*}

void ConstDecl() throws YAPLException :
{}{
  < CONST > <IDENT > "=" < Literal > ";"}

void VarDecl() throws YAPLException :
{
  Token t;
  Token x = null;
  ArrayList<Token> tokens = new ArrayList<Token>(); 
}{
  t = < IDENT >
  ("," x = < IDENT >
  {
    tokens.add(x);  })* ":" Type() ";"
  {
    symbolTable.addSymbol(new SymbolImpl(t.image, Symbol.SymbolKind.Variable));
    for(Token i : tokens) {
      symbolTable.addSymbol(new SymbolImpl(i.image, Symbol.SymbolKind.Variable));
    }  }}

void Decl() throws YAPLException :
{}
{
  < DECLARE > ( ConstDecl() | VarDecl() )*}

void Assignment() throws YAPLException :
{}{
  < IDENT > (Selector())? ":=" Expr()}
void IfStatement() throws YAPLException :
{}
{
  < IF > Expr() < THEN > StatementList() (< ELSE > StatementList())? < ENDIF >}

void WhileStatement() throws YAPLException :
{}{
  < WHILE > Expr() < DO > StatementList() < ENDWHILE >}

void ReturnStatement() throws YAPLException :
{}{
  < RETURN > (Expr())?}

void WriteStatement() throws YAPLException :
{}{
  < WRITE > < STRING >}

void Statement() throws YAPLException :{}{
  IfStatement() | WhileStatement() | ReturnStatement() | WriteStatement() | LOOKAHEAD(2) Assignment() |
  ProcedureCall() | Block()}

void StatementList() throws YAPLException :
{}
{
  (Statement() ";")*}

void Block() throws YAPLException :
{}
{
  {
    symbolTable.openScope(false);  }
  (Decl())? < BEGIN > StatementList() < END >
  {
    symbolTable.closeScope();  }}

void FormalParam() throws YAPLException :
{}
{
  < IDENT > ("," < IDENT >)* ":" (< READONLY >)? Type()}

void FormalParamList() throws YAPLException :
{}{
  FormalParam() (";" FormalParam())*}

void Procedure() throws YAPLException :
{}
{
  {
    symbolTable.openScope(false);  }
  < PROCEDURE > < IDENT > "(" (FormalParamList())? ")"
  (":" Type())? Block() < IDENT > ";"
  {
    symbolTable.closeScope();  }}

void Program() throws YAPLException :
{}
{
  {
    symbolTable.openScope(false);  }
  < PROGRAM > <IDENT > (Decl() | Procedure())*
  < BEGIN > StatementList() < END > <IDENT > "."
  {
    symbolTable.closeScope();  }}

void Start() throws YAPLException :{  Token t;}{
  {
    symbolTable.setDebug(true);  }
  (    Program()
  )*
  <EOF>}