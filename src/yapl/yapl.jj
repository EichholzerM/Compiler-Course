/**** YAPL Compiler
*
*	Copyright 2010 Daniel Hölbling, Klagenfurt University
*
*	Licensed under the Apache License, Version 2.0 (the "License");
*	you may not use this file except in compliance with the License.
*	You may obtain a copy of the License at
*
*		http://www.apache.org/licenses/LICENSE-2.0
*       
*	Unless required by applicable law or agreed to in writing, software
*	distributed under the License is distributed on an "AS IS" BASIS,
*	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
*	See the License for the specific language governing permissions and
*	limitations under the License.
***/
options {
  JDK_VERSION = "1.5";
}
PARSER_BEGIN(yapl)
package yapl;
public class yapl {
}
PARSER_END(yapl)

SKIP :
{
 	" "
|	"\r"
|	"\t"
|	"\n"
}

TOKEN:
{
  <# LETTER : (["A"-"Z"] | ["a"-"z"] | "_" ) > |
  <# DIGIT : (["0"-"9"]) > |
  <# OTHERCHAR: ("+" | "-" | "*" | "/" | "." | "," | ";" | ":" | "!"
| "?" | "=" | "#" | "%" | "<" | ">" | "$" | "(" | ")"
| "[" | "]" | "{" | "}" | "\\" | "@" | "&" | "^" | "|") >}

TOKEN :
{
    < COMMENT : ("/*" (~["*"] | "*" ~["/"])* "*/") > | 
  	< NEW: "new" > |
	< AND: "And" > |
	< OR: "Or" > |
	< DECLARE : "Declare" > |
	< PROGRAM : "Program" > |
	< BEGIN : "Begin" > |
	< END: "End" > |
	< IF : "If" > |
	< THEN : "Then" > |
	< ELSE : "Else" > |
	< ENDIF : "EndIf" > |
	< WHILE : "While" > |
	< CONST : "Const" > |
	< DO : "Do" > |
	< ENDWHILE : "EndWhile" > |
	< RETURN : "Return" > |
	< WRITE : "Write" > |
	< READONLY : "Readonly" > |
	< PROCEDURE : "Procedure" > |
	< Type : < PrimType > ( "[" "]")* > |
  	< PrimType : "Integer" | "Boolean" > |
  	< Literal: "True" | "False" | < NUMBER > > |	< IDENT : < LETTER > (< LETTER > | < DIGIT >)* > |
	< NUMBER: (< DIGIT >)+ > |
	< STRING: "\"" ( " " | < LETTER > | < DIGIT > | < OTHERCHAR > )* "\"" > 
}

void Selector() :
{}
{
  "[" Expr() "]" (Selector())?}

void ArrayLen() :
{}{
  "#" < IDENT > (Selector())?}
void PrimaryExpr() :
{}{
  (< Literal > | "(" Expr() ")" | LOOKAHEAD(2) ProcedureCall() | < IDENT > (Selector())? | ArrayLen())}
void ProcedureCall() :
{}
{
  < IDENT > "(" (ArgumentList())? ")"}
void ArgumentList() :
{}
{
  Expr() ("," Expr())*}
void UnaryExpr ():
{}
{
  (AddOp())? PrimaryExpr()    }
void MulExpr ():
{}
{
  (UnaryExpr()) (MulOp() UnaryExpr())*    }
void AddExpr ():
{}
{
  MulExpr() (AddOp() MulExpr())*}

void RelExpr ():
{}
{
  AddExpr() (RelOp() AddExpr())*}

void AddOp ():
{}
{
  "+" | "-"}

void MulOp ():
{}
{
  "*" | "/" | "%"}

void RelOp():
{}
{
   "<" | "<=" | ">=" | ">"}

void EqualOp():
{}
{
    "==" | "!="}
void EqualExpr ():
{}
{
  RelExpr() (EqualOp() RelExpr())*}

void CondAndExpr ():
{}
{
  EqualExpr() (< AND > EqualExpr())*}
void Expr():
{}{
  (CondAndExpr() (< OR > CondAndExpr())*) | CreationExpr()}
void CreationExpr():
{}{
  < NEW > < PrimType > "[" Expr() "]" ("[" Expr() "]")*}

void ConstDecl() :
{}{
  < CONST > <IDENT > "=" < Literal > ";"}

void VarDecl() :
{}{
  < IDENT > ("," < IDENT >)* ":" < Type > ";"}

void Decl() :
{}
{
  < DECLARE > ( ConstDecl() | VarDecl() )*}

void Assignment() :
{}{
  < IDENT > (Selector())? ":=" Expr()}
void IfStatement() :
{}
{
  < IF > Expr() < THEN > StatementList() (< ELSE > StatementList())? < ENDIF >}

void WhileStatement() :
{}{
  < WHILE > Expr() < DO > StatementList() < ENDWHILE >}

void ReturnStatement() :
{}{
  < RETURN > (Expr())?}

void WriteStatement() :
{}{
  < WRITE > < STRING >}

void Statement() :{}{
  IfStatement() | WhileStatement() | ReturnStatement() | WriteStatement() | LOOKAHEAD(2) Assignment() |
  ProcedureCall() | Block()}

void StatementList():
{}
{
  (Statement() ";")*}

void Block() :
{}
{
  (Decl())? < BEGIN > StatementList() < END >}

void FormalParam() :
{}
{
  < IDENT > ("," < IDENT >)* ":" (< READONLY >)? < Type >}

void FormalParamList() :
{}{
  FormalParam() (";" FormalParam())*}

void Procedure() :
{}
{
  < PROCEDURE > < IDENT > "(" (FormalParamList())? ")"
  (":" < Type >)? Block() < IDENT > ";"}

void Program() :
{}
{
  < PROGRAM > <IDENT > (Decl() | Procedure())*
  < BEGIN > StatementList() < END > <IDENT > "."}

void Start() :{  Token t;}{
  (
    < COMMENT > |     Program()
  )*
  {
	  do	  {	    t = getNextToken();
	    //System.out.println(t.kind + " : " + t.toString());
	  } while (t.kind != EOF);
  }
  <EOF>}